class_name CCU02StardustBoost
extends Upgrade
## CCU02 Upgrade: Increases the amount of stardust generated per second.

## Max Level.
var max_level : int = 5

## Initialize values.
func _init() -> void:
	title = "Stardust Generator Efficiency"
	base_cost = 1
	description_text = "Increases the amount of Stardust generated by the Universe Core."
	effect_text = "+1 stardust / sec"
	cost = 1
	level = Game.ref.data.cc_upgrades.u_02_stardust_boost_level


## Override description to show Consciousness Core cost instead of Stardust
func description() -> String:
	var _description : String = description_text
	_description += "\n[b]Effect : [/b] %s" % effect_text
	if level < max_level:
		_description += "\n[b]Cost : [/b] %s Consciousness Core" % cost
	return _description

## Returns whether or not the player can afford buying the upgrade.
func can_afford() -> bool:
	if level >= max_level:
		return false
		
	if Game.ref.data.consciouness_core >= cost:
		return true
		
	else:
		return false

## Consumes Consciouness Core to level up.
func level_up() -> void:
	if level >= max_level:
		return
	
	var error : Error = HandlerConsciounessCore.ref.consumed_consciouness_core(cost)
	if error:
		return
	
	level += 1
	Game.ref.data.cc_upgrades.u_02_stardust_boost_level = level
	
	leveled_up.emit()
	HandlerCCUpgrades.ref.upgrade_leveled_up.emit(self)
